plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'jacoco'
    id 'checkstyle'
    id "org.sonarqube" version "5.1.0.4882"
}

sonar {
 properties {
   property "sonar.projectName","eauchart_backend"
   property "sonar.projectKey","eauchart_backend"
   property "sonar.qualitygate.wait", true 
   property "sonar.sources", "src/main/java"
   property "sonar.java.binaries", "build/classes/java/main"
   property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
 }
}


group = 'com.example.cis4900.spring'
version = '1.0'
archivesBaseName="notes-app"


repositories {
    mavenCentral()
    jcenter()
}

jacoco {
        toolVersion = "0.8.11"
}
dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.mapstruct:mapstruct:1.6.2"
    annotationProcessor "org.mapstruct:mapstruct-processor:1.6.2"
    runtimeOnly 'mysql:mysql-connector-java:8.0.11'
    runtimeOnly 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
    implementation 'com.h2database:h2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-core:3.11.2' // Adding Mockito dependency for testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1' // Adding JUnit 5 dependency for testing
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1' // Adding JUnit 5 engine for testing
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run

}
 jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
        csv.required = true
        html.required = true
     }

 }

 
// Google Checkstyle is used
// No warnings are allowed
checkstyle {
    toolVersion = '10.18.1'
    configFile = file("config/checkstyle/checkstyle.xml")
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required.set(true)  // Generates XML report
        html.required.set(true) // Generates HTML report
    }
}
